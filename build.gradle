apply plugin: 'groovy'
apply plugin: 'idea'
apply plugin: 'maven'
apply plugin: 'signing'

version = '1.0-SNAPSHOT'
group = 'de.felixschulze.gradle'

ext.archivesBaseName = 'gradle-xcode-plugin'
ext.isSnapshot = version.endsWith("-SNAPSHOT")

repositories {
    mavenCentral()
}

dependencies {
    groovy localGroovy()
    compile gradleApi()
    compile 'org.eclipse.jgit:org.eclipse.jgit:2.3.1.201302201838-r'
    compile 'xmlwise:xmlwise:1.2.11'
    compile 'de.felixschulze.teamcity:teamcity-status-message-helper:0.1+'
}

task sourceJar(type: Jar) {
    description = 'An archive of the source code for Maven Central'
    classifier = 'sources'
    from sourceSets.main.groovy
}

task groovydocJar(type: Jar) {
    description = 'An archive of the GroovyDocs for Maven Central'
    classifier = 'javadoc'
    from groovydoc
}

artifacts {
    archives groovydocJar, sourceJar
}

signing {
    sign configurations.archives
}

// Only *require* signing if we are uploading a non snapshot version
gradle.taskGraph.whenReady { taskGraph ->
    tasks.withType(org.gradle.plugins.signing.Sign).all {
        required = taskGraph.hasTask(":uploadArchives") && !isSnapshot
    }
}


install.repositories.mavenInstaller {
    pom.project(pomConfiguration)
    if (signing.signatory) {
        beforeDeployment { signing.signPom(it) }
    }
}

uploadArchives {
    repositories.mavenDeployer {
        if (signing.signatory) {
            beforeDeployment { signPom(it) }
        }
        name = 'mavenCentralReleaseDeployer'
        repository(url: 'https://oss.sonatype.org/service/local/staging/deploy/maven2/') {
            authentication(userName: System.properties['sonatypeUsername'], password: System.properties['sonatypePassword'])
            releases(updatePolicy: 'always')
            snapshots(updatePolicy: 'always')
        }
        pom.project(pomConfiguration)
    }
}

/**v
 * Create POM config and return for use by other tasks.
 */
def getPomConfiguration() {
    return {
        name 'Gradle Xcode Plugin'
        packaging 'jar'
        description 'A gradle plugin for building xcode projects.'
        url 'https://github.com/x2on/gradle-xcode-plugin'
        licenses {
            license {
                name 'The MIT License'
                url 'http://www.opensource.org/licenses/mit-license.php'
                distribution 'repo'
            }
        }
        developers {
            developer {
                id 'x2on'
                name 'Felix Schulze'
                email 'code@felixschulze.de'
            }
        }
        scm {
            connection 'scm:https://x2on@github.com/x2on/gradle-xcode-plugin'
            developerConnection 'scm:git@github.com:x2on/gradle-xcode-plugin.git'
            url 'https://github.com/x2on/gradle-xcode-plugin'
        }
    }
}